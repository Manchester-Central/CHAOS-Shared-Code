buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.apache.velocity:velocity-engine-core:2.3'
    }
}

plugins {
    id "java"
    id 'maven-publish'
    id "edu.wpi.first.GradleRIO" version "2026.2.1"
    id "com.peterabeles.gversion" version "1.10"
    id 'com.diffplug.spotless' version '6.20.0'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

java {
    withSourcesJar()
    withJavadocJar()
}

repositories {
    jcenter()
    maven {
        url 'https://frcmaven.wpi.edu/artifactory/release'
    }
}

repositories {
    maven {
        url = uri("https://maven.pkg.github.com/Mechanical-Advantage/AdvantageKit")
        credentials {
            username = "Mechanical-Advantage-Bot"
            password = "\u0067\u0068\u0070\u005f\u006e\u0056\u0051\u006a\u0055\u004f\u004c\u0061\u0079\u0066\u006e\u0078\u006e\u0037\u0051\u0049\u0054\u0042\u0032\u004c\u004a\u006d\u0055\u0070\u0073\u0031\u006d\u0037\u004c\u005a\u0030\u0076\u0062\u0070\u0063\u0051"
        }
    }
    mavenLocal()
}

task(replayWatch, type: JavaExec) {
    mainClass = "org.littletonrobotics.junction.ReplayWatch"
    classpath = sourceSets.main.runtimeClasspath
}

tasks.withType(JavaCompile) {
    options.compilerArgs.add '-XDstringConcat=inline'
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc).configureEach {
    options.encoding = 'UTF-8'
}

dependencies {
    annotationProcessor wpi.java.deps.wpilibAnnotations()
    implementation wpi.java.deps.wpilib()
    implementation wpi.java.vendor.java()

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // AdvantageKit Lines
    def akitJson = new groovy.json.JsonSlurper().parseText(new File(projectDir.getAbsolutePath() + "/vendordeps/AdvantageKit.json").text)
    annotationProcessor "org.littletonrobotics.akit:akit-autolog:$akitJson.version"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.chaos131'
            artifactId = 'shared'
            version = "$System.env.RELEASE_VERSION"

            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/Manchester-Central/CHAOS-Shared-Code"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

spotless {
    java {
        target fileTree('.') {
            include '**/*.java'
            exclude '**/build/**', '**/build-*/**', 'bin/generated-sources/**', 'src/main/java/com/chaos131/SharedCodeBuildConstants.java'
        }
        toggleOffOn()
        googleJavaFormat()
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
    groovyGradle {
        target fileTree('.') {
            include '**/*.gradle'
            exclude '**/build/**', '**/build-*/**'
        }
        greclipse()
        indentWithSpaces(4)
        trimTrailingWhitespace()
        endWithNewline()
    }
    format 'xml', {
        target fileTree('.') {
            include '**/*.xml'
            exclude '**/build/**', '**/build-*/**'
        }
        eclipseWtp('xml')
        trimTrailingWhitespace()
        indentWithSpaces(2)
        endWithNewline()
    }
    format 'misc', {
        target fileTree('.') {
            include '**/*.md', '**/.gitignore'
            exclude '**/build/**', '**/build-*/**'
        }
        trimTrailingWhitespace()
        indentWithSpaces(2)
        endWithNewline()
    }
}

// Codegen
import org.apache.velocity.app.VelocityEngine
import org.apache.velocity.VelocityContext
tasks.register('generatePoseStructures') {
    group = 'Generation'

    // Define the data to loop over
    def configs = [
        // [
        //     'Codegen2024',
        //     'MirroredFieldPose',
        //     'frc2024.fmap'
        // ],
        [
            'Codegen2025',
            'PivotedFieldPose',
            'frc2025r2_ANDYMARK.fmap'
        ],
        [
            'Codegen2026',
            'PivotedFieldPose',
            'FRC2026_ANDYMARK.fmap'
        ],
    ]

    // Track the template as input and a folder as output
    def templateFile = file('./src/main/java/com/chaos131/poses/FieldPoseTemplate.vm')

    doLast {
        def ve = new org.apache.velocity.app.VelocityEngine()
        ve.init()

        configs.each { config ->
            def (name, parent_class, json_filename) = config
            def json_filepath = file("src/main/deploy/${json_filename}")

            if (!json_filepath.exists()) {
                return;
            }

            def json_contents = new groovy.json.JsonSlurper().parseText(json_filepath.text)
            def field_length = json_contents.fieldlength
            def field_width = json_contents.fieldwidth

            // Create a unique file for each entry
            def outputFile = file("src/main/java/com/chaos131/poses/${name}.java")

            def tag_strings = []
            for (fiducial in json_contents.fiducials) {
                double[] transform_array = fiducial.transform.collect { it as double }
                tag_strings.add("public static final ${name} Tag${fiducial.id} = new ${name}(\"Tag${fiducial.id}\", MatBuilder.fill(Nat.N4(), Nat.N4(), new double[]{${transform_array.join(", ")}}));")
            }

            def context = new org.apache.velocity.VelocityContext()
            context.put("className", name)
            context.put("parentClass", parent_class)
            context.put("fieldLength", field_length)
            context.put("fieldWidth", field_width)
            context.put("aprilTags", tag_strings.join('\n'))

            outputFile.withWriter { writer ->
                ve.evaluate(context, writer, "LOG", templateFile.text)
            }
        }
    }
}

// Codegen

test {
    useJUnitPlatform()
    systemProperty 'junit.jupiter.extensions.autodetection.enabled', 'true'
}
wpi.java.configureTestTasks(test)

project.compileJava.dependsOn(createVersionFile)
project.compileJava.dependsOn(generatePoseStructures)
project.compileJava.dependsOn 'spotlessApply'
gversion {
    srcDir       = "src/main/java/"
    classPackage = "com.chaos131"
    className    = "SharedCodeBuildConstants"
    dateFormat   = "yyyy-MM-dd HH:mm:ss z"
    timeZone     = "America/New_York" // Use preferred time zone
    language     = "java"
    indent       = "    "
}
